
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс 


Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт	
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.РеализацияТоваровУслуг"; 
	КомандаПечати.Идентификатор = "АктВыполненныхРабот";
	КомандаПечати.Представление = НСтр("ru = 'Печать акта выполненных работ'");
	КомандаПечати.Порядок = 5; 
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АктВыполненныхРабот");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = СформироватьПечатнуюФормуАктаРабот(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Печать акта выполненных работ'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.РеализацияТоваровУслуг.ПФ_MXL_АктВыполненныхРабот";
	КонецЕсли;   
	
КонецПроцедуры 

Функция СформироватьПечатнуюФормуАктаРабот(МассивОбъектов, ОбъектыПечати)    
	 ТабличныйДокумент = Новый ТабличныйДокумент;
	 ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АктВыполненныхРабот";  
	 
	 Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ПФ_MXL_АктВыполненныхРабот");   
	 
	 Запрос = Новый Запрос;
	 Запрос.Текст = "ВЫБРАТЬ
	                |	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	                |	РеализацияТоваровУслуг.Номер КАК НомерДокумента,
	                |	РеализацияТоваровУслуг.Дата КАК ДатаДокумента,
	                |	РеализацияТоваровУслуг.Организация.Наименование КАК Организация,
	                |	РеализацияТоваровУслуг.Контрагент.Наименование КАК Контрагент,
	                |	РеализацияТоваровУслуг.Товары.(
	                |		Номенклатура КАК Номенклатура,
	                |		Количество КАК Количество,
	                |		Цена КАК Цена,
	                |		Сумма КАК Сумма
	                |	) КАК Товары,
	                |	РеализацияТоваровУслуг.Услуги.(
	                |		Номенклатура КАК Номенклатура,
	                |		Количество КАК Количество,
	                |		Цена КАК Цена,
	                |		Сумма КАК Сумма
	                |	) КАК Услуги,
	                |	РеализацияТоваровУслуг.СуммаДокумента КАК СуммаИтог,
	                |	РеализацияТоваровУслуг.Договор.Наименование КАК Договор
	                |ИЗ
	                |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	                |ГДЕ
	                |	РеализацияТоваровУслуг.Ссылка В(&МассивОбъектов)"; 
	 
	 Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	 РезультатЗапроса = Запрос.Выполнить();
	 Шапка = РезультатЗапроса.Выбрать();
	 
	 ПервыйДокумент = Истина;   
	 
	 Пока Шапка.Следующий() Цикл  
		 
		 Если НЕ ПервыйДокумент Тогда
			 //Все документы надо выводить на разных страницах
			 ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		 КонецЕсли;     
		 
		 ПервыйДокумент = Ложь;    
		 
		 СписокПолей = "";
		 Для Каждого Колонка из РезультатЗапроса.Колонки Цикл 
			СписокПолей = СписокПолей + ?(СписокПолей = "", "", ", ") + Колонка.Имя; 
		 КонецЦикла;
		 
		 //Запомним номер строки с которой начали выводить текущий документ
		 НомерСтрокиНачала = ТабличныйДокумент.ВысотаТаблицы + 1;
		 
		 //Начало вывода документа
		 Область = Макет.ПолучитьОбласть("Шапка");   
		 ДанныеЗаполнения = Новый Структура(СписокПолей);
		 ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, Шапка);
		 ДанныеЗаполнения.ДатаДокумента = Формат(ДанныеЗаполнения.ДатаДокумента,"ДЛФ=DD");
		 Область.Параметры.Заполнить(ДанныеЗаполнения);
		 ТабличныйДокумент.Вывести(Область);
		 
		 Область = Макет.ПолучитьОбласть("ШапкаТаблицы"); //без параметров
		 ТабличныйДокумент.Вывести(Область);   
		 
		 //Заполнение параметров таблице макета ----------------------
		 РезультатТовары = Шапка.Товары;
		 РезультатУслуги = Шапка.Услуги;
		 
		 СписокПолейТЧ = "";
		 Для Каждого Колонка из РезультатТовары.Колонки Цикл 
			СписокПолейТЧ = СписокПолейТЧ + ?(СписокПолейТЧ = "", "", ", ") + Колонка.Имя; 
		КонецЦикла;
		
		 Товары = РезультатТовары.Выбрать(); 
		 Пока Товары.Следующий() Цикл 
			 Область = Макет.ПолучитьОбласть("СтрокаНоменклатуры");
			 ДанныеЗаполненияТовары = Новый Структура(СписокПолейТЧ); 
			 ЗаполнитьЗначенияСвойств(ДанныеЗаполненияТовары,Товары); 
			 Область.Параметры.Заполнить(ДанныеЗаполненияТовары);
			 ТабличныйДокумент.Вывести(Область);
		 КонецЦикла; 
		 
		 Услуги = РезультатУслуги.Выбрать(); 
		 Пока Услуги.Следующий() Цикл 
			 Область = Макет.ПолучитьОбласть("СтрокаНоменклатуры");
			 ДанныеЗаполненияУслуги = Новый Структура(СписокПолейТЧ); 
			 ЗаполнитьЗначенияСвойств(ДанныеЗаполненияУслуги,Услуги); 
			 Область.Параметры.Заполнить(ДанныеЗаполненияУслуги);
			 ТабличныйДокумент.Вывести(Область);
		 КонецЦикла;
		 //Конец заполнения параметров таблицы макета ----------------
		 
		 Область = Макет.ПолучитьОбласть("Итоги");
		 Область.Параметры.Заполнить(ДанныеЗаполнения);  
		 ПараметрыВалюты = "рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2";
		 Область.Параметры.СуммаПрописью = ЧислоПрописью(Шапка.СуммаИтог,, ПараметрыВалюты);
		 ТабличныйДокумент.Вывести(Область); 
		 
		 Область = Макет.ПолучитьОбласть("Подписи"); //без параметров
		 ТабличныйДокумент.Вывести(Область);
		 
		 Область = Макет.ПолучитьОбласть("МестоДляПечати");  //без параметров
		 ТабличныйДокумент.Вывести(Область);
		 //Конец вывода документа
		 
		 //В табличном документе необходимо задать имя области, в которую был 
		 // выведен объект. Нужно для возмождности печати комплектов документов
		 УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент,НомерСтрокиНачала, ОбъектыПечати, Шапка.Ссылка);
		 
	 КонецЦикла; 
	 
	 Возврат ТабличныйДокумент;
		 		 
КонецФункции

#КонецОбласти

#КонецЕсли