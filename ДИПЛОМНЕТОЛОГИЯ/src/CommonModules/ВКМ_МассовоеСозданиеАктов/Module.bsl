#Область ПрограмныйИнтерфейс    

// Создать акты выполненных работ по договору "Абонентское обслуживание"
// Возвращаемое значение - массив записей
Функция СоздатьАктыПоДоговорамАбонентскойПлаты(Период, ДатаАктов) Экспорт   

	Если Не ЗначениеЗаполнено(Период) ИЛИ НЕ ЗначениеЗаполнено(ДатаАктов) Тогда  
		ОбщегоНазначения.СообщитьПользователю("Не выбран период или не указана дата создаваемых документов "); 
		Возврат Неопределено;
	КонецЕсли;
	
	// Ищем ДоговорыСсылки которые не попадают в список договоров по которым в указанный период уже есть реализации. 
	// И по этим договорам создаем и заполняем АВРы 
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Договор,
	|	ДоговорыКонтрагентов.Организация КАК Организация,
	|	ДоговорыКонтрагентов.ВКМ_АбонентскаяПлата КАК АбонентскаяПлата,
	|	ДоговорыКонтрагентов.ВКМ_СтоимостьЧасаРаботыСпециалиста КАК СтоимостьЧасаРаботыСпециалиста,
	|	ДоговорыКонтрагентов.Владелец КАК Контрагент
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.ВидыДоговоровКонтрагентов.АбонентскоеОбслуживание)
	|	И НЕ ДоговорыКонтрагентов.Ссылка В
	|		(ВЫБРАТЬ
	|			РеализацияТоваровУслуг.Договор КАК Договор
	|		ИЗ
	|			Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ГДЕ
	|			РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачало И &ДатаКонец
	|			И
	|				РеализацияТоваровУслуг.Договор.ВидДоговора = ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.ВидыДоговоровКонтрагентов.АбонентскоеОбслуживание))
	|	И ДоговорыКонтрагентов.ВКМ_НачалоДействияДоговора < &ДатаАкта
	|	И ДоговорыКонтрагентов.ВКМ_КонецДействияДоговора > &ДатаАкта";
	
	Запрос.УстановитьПараметр("ДатаАкта", ДатаАктов);
	Запрос.УстановитьПараметр("ДатаНачало", НачалоМесяца(Период.ДатаНачала));
	Запрос.УстановитьПараметр("ДатаКонец", КонецМесяца(Период.ДатаОкончания));
	
	Выборка = Запрос.Выполнить().Выбрать();  
	
	//Выгрузка результата функции в массив: эл-т массива Структура СсылкаДокумент и ДоговорСсылка
	СозданныеАВР = Новый Массив;  
	
	Пока Выборка.Следующий() Цикл 
		НовыйДокументАВР = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		НовыйДокументАВР.Дата = ДатаАктов;
		НовыйДокументАВР.Организация = Выборка.Организация;
		НовыйДокументАВР.Контрагент = Выборка.Контрагент;
		НовыйДокументАВР.Договор = Выборка.Договор;
		НовыйДокументАВР.ВКМ_ЗаполнитьНаСервере(НовыйДокументАВР); 
		НовыйДокументАВР.Записать(РежимЗаписиДокумента.Проведение,РежимПроведенияДокумента.Неоперативный);
		
		Запись = Новый Структура;
		Запись.Вставить("Ссылка", НовыйДокументАВР.Ссылка);
		Запись.Вставить("Договор",НовыйДокументАВР.Договор);
		СозданныеАВР.Добавить(Запись);  
		
	КонецЦикла;  

	Возврат ?(ЗначениеЗаполнено(СозданныеАВР) , СозданныеАВР, Неопределено);	
  	
КонецФункции
#КонецОбласти 




